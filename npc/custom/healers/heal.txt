//===== eAthena Script =======================================
//= Healer
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= Basic healer script.
//============================================================

-	script	Healer	-1,{
	
	set .@Price,0;	// Zeny required for heal
	set .@Buffs,0;	// Also buff players? (1: yes / 0: no)
	set .@Delay,0;	// Heal delay, in seconds

	callfunc "F_ClearGarbage",0;
	if (@HD > gettimetick(2)) end;
	if (.@Price) {
		message strcharinfo(0),"Healing costs "+.@Price+" Zeny.";
		if (Zeny < .@Price) end;
		if(select("^0055FFHeal^000000:^777777Cancel^000000")==2) close;
		set Zeny, Zeny-.@Price;
	}
	specialeffect2 313; percentheal 100,100;
	if (.@Buffs) {
		specialeffect2 37; sc_start SC_INCREASEAGI,240000,10;
		specialeffect2 42; sc_start SC_BLESSING,240000,10;
	}
	if (.@Delay) set @HD, gettimetick(2)+.@Delay;
	close;
}

alberta,185,144,5	duplicate(Healer)	Healer#alb	749
aldebaran,134,123,5	duplicate(Healer)	Healer#alde	749
amatsu,200,80,5	duplicate(Healer)	Healer#ama	749
ayothaya,155,111,5	duplicate(Healer)	Healer#ayo	749
comodo,188,162,5	duplicate(Healer)	Healer#com	749
geffen,121,61,5	duplicate(Healer)	Healer#gef	749
gonryun,164,130,5	duplicate(Healer)	Healer#gon	749
izlude,125,118,5	duplicate(Healer)	Healer#izl	749
louyang,225,103,5	duplicate(Healer)	Healer#lou	749
morocc,159,96,5	duplicate(Healer)	Healer#mor	749
niflheim,188,180,5	duplicate(Healer)	Healer#nif	749
payon,180,105,5	duplicate(Healer)	Healer#pay	749
prontera,157,187,4	duplicate(Healer)	Healer#pront	749
umbala,94,162,5	duplicate(Healer)	Healer#umb	749
xmas,149,136,5	duplicate(Healer)	Healer#xmas	749
yuno,152,186,5	duplicate(Healer)	Healer#yuno	749